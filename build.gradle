plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id "com.diffplug.gradle.spotless" version "3.25.0"
    id "de.undercouch.download" version "4.0.0"
    id "com.jfrog.bintray" version "1.8.4"
}

ext {
    bouncycastleVersion = '1.58.0.0'
    jacksonVersion = '2.8.5'
    javaPoetVersion = '1.7.0'
    kotlinPoetVersion = '1.5.0'
    jnr_unixsocketVersion = '0.21'
    okhttpVersion = '4.3.1'
    rxjavaVersion = '2.2.2'
    slf4jVersion = '1.7.25'
    javaWebSocketVersion = '1.3.8'
    picocliVersion = '3.0.0'
    web3jUnitVersion = version
    // test dependencies
    equalsverifierVersion = '3.1.10'
    junitVersion = '5.5.2'
    junitBenchmarkVersion = '0.7.2'
    logbackVersion = '1.2.3'
    mockitoJunitVersion = "3.1.0"
    junitPlatformLauncherVersion = "1.5.2"
}


description 'web3j base project'

[
    'bintray',
    'jacoco',
    'java',
    'javadoc',
    'repositories',
    'spotless'
].each { buildScript ->
    download {
        src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
        dest "$rootDir/gradle/$buildScript/build.gradle"
        overwrite true
        quiet true
        onlyIfModified true
    }
}

allprojects {
    apply {
        [
            'java',
            'javadoc',
            'repositories',
            'spotless'
        ].each { buildScript ->
            from("$rootDir/gradle/$buildScript/build.gradle")
        }
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
        useJUnitPlatform()
    }

    test {
        testLogging {
            exceptionFormat = 'full'
        }
    }

    dependencies {
        testCompile "org.junit.jupiter:junit-jupiter:$junitVersion"
        testCompile "org.mockito:mockito-junit-jupiter:$mockitoJunitVersion"
    }
}
configure(subprojects.findAll { it.name != 'integration-tests' }) {
    apply from: "$rootDir/gradle/jacoco/build.gradle"
}

bintrayUpload {
    enabled = false
}
